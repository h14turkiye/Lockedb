plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'pl.allegro.tech.build.axion-release' version '1.18.7'
    id 'com.gradleup.shadow' version '8.3.3'
}

group = 'com.github.h14turkiye'
version = scmVersion.version

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'java'
    apply plugin: 'com.gradleup.shadow'

    group = rootProject.group
    version = rootProject.version

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = 'https://jitpack.io' }
        maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencies {
        compileOnly 'io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT'
        implementation 'com.github.technicallycoded:FoliaLib:main-SNAPSHOT'
    }

    tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
        relocate 'com.tcoded.folialib', 'com.h14turkiye.mcschedlock.relocated.github.tcoded.folialib'
        archiveClassifier = ''
    }

    assemble.dependsOn shadowJar

    jar {
        from project(':core').sourceSets.main.output  // Include :core classes in the JAR
        archiveClassifier = 'not-shaded'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifactId = "mcschedlock-${project.name}"
            }
        }
    }
}

tasks.named('assemble') {
    dependsOn subprojects*.tasks*.assemble
}

project(':mongodb') {
    dependencies {
        implementation project(':core')
        implementation 'org.mongodb:bson:4.9.1'
        implementation 'org.mongodb:mongo-java-driver:3.12.10'

        testImplementation('org.mockbukkit.mockbukkit:mockbukkit-v1.21:4.3.1')
        testImplementation(platform('org.junit:junit-bom:5.11.3'))
	    testImplementation('org.junit.jupiter:junit-jupiter')
	    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
    }

    tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
        relocate 'org.bson', 'com.h14turkiye.mcschedlock.relocated.org.bson'
        relocate 'com.mongodb', 'com.h14turkiye.mcschedlock.relocated.com.mongodb'
    }

    tasks.test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

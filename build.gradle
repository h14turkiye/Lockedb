plugins {
    id 'java'
    id 'maven-publish'
    id 'pl.allegro.tech.build.axion-release' version '1.18.7'
}

group = 'com.github.h14turkiye'
version = scmVersion.version

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'java'

    group = rootProject.group
    version = rootProject.version

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = 'https://jitpack.io' }
        maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.36'
	    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    }

    jar {
        from project(':core').sourceSets.main.output  // Include :core classes in the JAR
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifactId = "lockedb-${project.name}"
            }
        }
    }
}

tasks.named('assemble') {
    dependsOn subprojects*.tasks*.assemble
}

project(':mongodb') {
    dependencies {
        implementation project(':core')
        implementation 'org.mongodb:bson:4.9.1'
        implementation 'org.mongodb:mongo-java-driver:3.12.10'

        testImplementation(platform('org.junit:junit-bom:5.11.3'))
	    testImplementation('org.junit.jupiter:junit-jupiter')
	    testRuntimeOnly('org.junit.platform:junit-platform-launcher')

        testImplementation 'org.testcontainers:mongodb:1.20.6'
        testImplementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.24.3' 
        testImplementation 'org.apache.logging.log4j:log4j-api:2.24.3'
    }

    tasks.test {
        useJUnitPlatform()
        testLogging {
            showStandardStreams = true
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
    }
}
